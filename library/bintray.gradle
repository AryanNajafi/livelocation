apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

version mavenVersion
group mavenGroup

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        library(MavenPublication) {
            groupId mavenGroup
            artifactId mavenArtifactId
            version mavenVersion

            artifact(sourcesJar)
            artifact("$buildDir/outputs/aar/library-release.aar")

            pom.withXml {

                asNode().appendNode('name', "LiveLocation")
                asNode().appendNode('description', "An Android lifecycle-aware location library")
                asNode().appendNode('url', 'https://github.com/AryanNajafi/livelocation')
                asNode().appendNode('inceptionYear', '2019')

                def license = asNode().appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')

                def developer = asNode().appendNode('developers').appendNode('developer')
                developer.appendNode('name', 'Aryan Najafi')
                developer.appendNode('email', 'aryannjfi@gmail.com')

                def scm = asNode().appendNode('scm')
                scm.appendNode('connection', 'https://github.com/AryanNajafi/livelocation.git')
                scm.appendNode('url', 'https://github.com/AryanNajafi/livelocation')

                def dependenciesNode = asNode().getAt('dependencies')[0]
                if (dependenciesNode == null) {
                    dependenciesNode = asNode().appendNode('dependencies')
                }

                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = [ 'library' ]
    configurations = [ 'archives' ]

    pkg {
        repo = 'LiveLocation'
        name = mavenArtifactId
        licenses = [ 'Apache-2.0' ]
        vcsUrl = 'https://github.com/AryanNajafi/livelocation.git'

        version {
            name = mavenVersion
            released = new Date()
        }
    }
}